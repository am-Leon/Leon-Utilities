plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

ext {
    mGroupId = "am-leom"
    mArtifactId = "utilities"
    mVersionCode = 4
    mVersionName = "v0.2.0"

    mLibraryName = "Leon-Utilities"
    mLibraryDescription = "$mLibraryName Contains Utilities classses."
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(rootProject.file(path)))
    return props
}

android {
    compileSdk 33
    namespace 'am.leon.utilities'

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Android
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.activity:activity-ktx:1.6.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.3'
    implementation 'com.google.android.material:material:1.6.1'

    // Kotlin
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.7.10'

    // Google Gson
    implementation 'com.google.code.gson:gson:2.9.0'

    // Google Maps
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = mGroupId
                artifactId = mArtifactId
                version = mVersionName

                from components.release

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/am-Leon/Leon-Utilities")
                credentials {
                    def githubProp = getProps("$rootDir/github.properties")
                    username = githubProp['GPR_USER']
                    password = githubProp['GPR_KEY']
                }
            }
        }
    }
}

// Assembling should be performed before publishing package
publish.dependsOn assemble